plugins {
    id 'java-library'
}

processResources {
    // This will ensure that this task is redone when the versions change.
    inputs.property "version", project.version

    // Replace values in only mcmod.info.
    filesMatching('mcmod.info') {
        // Replace version
        expand 'version':project.version
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_6
    targetCompatibility = JavaVersion.VERSION_1_6
}

subprojects {
    apply plugin: "java"

    version = rootProject.version
    group = rootProject.group

    dependencies {
        compileOnly(rootProject)
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    // This task creates a .jar file containing the source code of this mod.
    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }

    // This task creates a .jar file containing a deobfuscated version of this mod, 
    // for other developers to use in a development environment.
    task devJar(type: Jar) {
        archiveClassifier.set("dev")
        from sourceSets.main.output
    }

    // Creates the listed artifacts on building the mod.
    artifacts {
        archives sourcesJar
        archives devJar
    }

    // the task to specify generating jar is finished
    task finishCreateJar
}

// then create fat jar now!

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
    subprojects.each {
        dependsOn(it.tasks.sourcesJar)
        from (zipTree(it.tasks.sourcesJar.archiveFile)) {
            exclude "META-INF", "META-INF/**"
        }
    }
    duplicatesStrategy = DuplicatesStrategy.FAIL
}

task devJar(type: Jar) {
    archiveClassifier.set("dev")
    from sourceSets.main.output
    subprojects.each {
        dependsOn(it.tasks.devJar)
        from (zipTree(it.tasks.devJar.archiveFile)) {
            exclude "META-INF", "META-INF/**"
        }
    }
    duplicatesStrategy = DuplicatesStrategy.FAIL
}

jar {
    subprojects.each {
        dependsOn(it.tasks.finishCreateJar)
        from (zipTree(it.tasks.jar.archiveFile)) {
            exclude "META-INF", "META-INF/**"
        }
    }
    duplicatesStrategy = DuplicatesStrategy.FAIL
}

artifacts {
    archives sourcesJar
    archives devJar
}
